@top Program { expression* }

expression { LineComment | TripleLineComment | Week | Day | ExerciseExpression | EmptyExpression }

ExerciseExpression { ExerciseName (SectionSeparator ExerciseSection?)* linebreakOrEof }
EmptyExpression { linebreak }

ExerciseName { NonSeparator+ }
ExerciseSets { CurrentVariation? ExerciseSet ("," ExerciseSet)* }
WarmupExerciseSets { (WarmupExerciseSet ("," WarmupExerciseSet)*) | None }
ExerciseSection {  ExerciseProperty | ExerciseSets }
ReuseSection { "..." ExerciseName? }
ExerciseSet { (Rpe | Timer | SetPart | Weight | Percentage | SetLabel)+ }
WarmupExerciseSet { (WarmupSetPart | Weight | Percentage)+ }
ExerciseProperty { ExercisePropertyName ":" (FunctionExpression | WarmupExerciseSets ) }
ExercisePropertyName { Keyword }
None { @specialize<Keyword, "none"> }
CurrentVariation { "!" }

FunctionExpression { 
  FunctionName
  ("(" FunctionArgument? ("," FunctionArgument)* ")")?
  (Liftoscript | ReuseLiftoscript)?
}
ReuseLiftoscript { "{" ReuseSection "}" }
FunctionName { Keyword }
FunctionArgument { Rep | Weight | Percentage | Rpe | RepRange | KeyValue }

Number { Float | Int }
RepRange { Rep "-" Rep }
Rep { Int }

Rpe { "@" Number "+"? }
Percentage { Number "%" }
Weight { Number ("kg" | "lb") }
Timer { Int "s" }
SetPart { Rep "+"? "x" (RepRange | Rep) "+"? }
KeyValue { Keyword ":" (Float | Int | Weight | Percentage) }
SetLabel { "(" NonSeparator+ ")" }

WarmupSetPart { (Rep "x")? Rep }

@skip { space }

@tokens {
  @precedence { Float, Int }
  @precedence { Day, Week }
  @precedence { TripleLineComment, LineComment }
  TripleLineComment { "///" ![\n]* linebreakOrEof }
  LineComment { "//" ![\n]* linebreakOrEof }
  Liftoscript { "{~" ![~]* "~}" }
  Week { "#" ![\n]* linebreakOrEof }
  Day { "##" ![\n]* linebreakOrEof }
  space { $[ \t]+ }
  SectionSeparator { "/" }
  linebreakOrEof { linebreak | @eof }
  linebreak { "\n" | "\r" | "\r\n" }
  NonSeparator { ![/{}() \t\n\r#]+ }
  Keyword { $[a-zA-Z_] $[0-9a-zA-Z_]* }
  Int { @digit+ }
  Float { @digit* "." @digit+ }
}